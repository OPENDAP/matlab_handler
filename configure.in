
dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Id: configure.in,v 1.1 1996/11/02 18:41:44 jimg Exp $)
AC_INIT(MATArray.cc)
AC_CONFIG_AUX_DIR(../../etc)

dnl the variable `dir' is used as a basename for the directory tar file

fulldir=`pwd`
dir=`basename ${fulldir}`
AC_SUBST(dir)
dods_root=`echo $fulldir | sed 's@\(.*\)/.*/.*@\1@'`
AC_DEFINE_UNQUOTED(DODS_ROOT, "$dods_root")

dnl the command to use to generate dependencies for makefiles

depend=${dods_root}/etc/depend.sh
AC_SUBST(depend)

AC_DEFINE(DVR, "DODS/2.0")
AC_DEFINE(CNAME, "DODS")
AC_DEFINE(CVER, "2.0")

dnl flags for the compilers and linkers - set these before locating the
dnl actual tools since some of the AC_PROG macros set these `flag variables'
dnl to default values otherwise.

AC_CANONICAL_HOST

CFLAGS="-g -O2"
CXXFLAGS="-g -O2"	
LDFLAGS="-g"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_AWK
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_MAKE_SET
DODS_PROG_LEX
DODS_PROG_BISON
AC_PROG_INSTALL

DODS_CHECK_GCC_DEBUG

if echo $INSTALL | egrep 'install-sh'
then
	INSTALL="$(DODS_ROOT)/etc/install-sh -c"
	AC_SUBST(INSTALL)
fi

dnl You should have g++, but you can override that with an environment
dnl variable. 

CXX=${CXX-g++}
AC_MSG_RESULT(Setting CXX to $CXX)
AC_SUBST(CXX)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Ignore the errors about AC_TRY_RUN missing an argument. jhrg 5/2/95

AC_C_CROSS

if test "$cross_compiling" = yes
then
	case "$host" in
	*alpha*) AC_DEFINE(SIZEOF_CHAR, 1)
		 AC_DEFINE(SIZEOF_DOUBLE, 8)
		 AC_DEFINE(SIZEOF_FLOAT, 4)
		 AC_DEFINE(SIZEOF_INT, 4)
		 AC_DEFINE(SIZEOF_LONG, 8)
		 ;;
	*)	AC_MSG_WARN(Assuming that your target is a 32bit machine)
		AC_DEFINE(SIZEOF_CHAR, 1)
		AC_DEFINE(SIZEOF_DOUBLE, 8)
		AC_DEFINE(SIZEOF_FLOAT, 4)
		AC_DEFINE(SIZEOF_INT, 4)
		AC_DEFINE(SIZEOF_LONG, 4)
		;;
	esac
else
	AC_CHECK_SIZEOF(int)
	AC_CHECK_SIZEOF(long)
	AC_CHECK_SIZEOF(char)
	AC_CHECK_SIZEOF(double)
	AC_CHECK_SIZEOF(float)
fi

DODS_SEM

DODS_OS

DODS_MATLAB

DODS_WWW_ROOT

DODS_FIND_EXPECT

DODS_EFENCE

DODS_DBNEW

DODS_DEBUG_OPTION

AC_OUTPUT(Makefile)
